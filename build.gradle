group 'com.sharpandrew'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath group: "com.bmuschko", name: "gradle-docker-plugin", version: "${gradleDockerPluginVersion}"
    }
}

apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: 'com.bmuschko.docker-java-application'
apply plugin: 'findbugs'
apply plugin: 'java'
apply plugin: 'pmd'

mainClassName = 'com.sharpandrew.learndropwizard.HelloWorldApplication'
sourceCompatibility = 1.8

run {
    if(System.getProperty("exec.args") != null) {
        args System.getProperty("exec.args").split()
    }
}

sourceSets {
    main.java.srcDir "src/main/java"
    main.resources.srcDir "src/main/resources"
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: "${dropwizardVersion}"
    testCompile group: 'junit', name: 'junit', version: "${junitVersion}"
}

checkstyle {
    configFile = file('checkstyle.xml')
    toolVersion = "${checkstyleVersion}"
}

// docker

configurations {
    dockerJava {
        resolutionStrategy {
            force 'de.gesellix:unix-socket-factory:2016-04-06T22-21-19'
        }
    }
}

def dockerTagName = "${group}/${name}:${version}"
def dockerPort = 8080

docker {
    javaApplication {
        baseImage = 'openjdk:8u102'
        maintainer = 'Andrew Sharp "me@sharpandrew.com"'
        ports = [dockerPort]
        tag = dockerTagName
    }
    url = 'unix:///var/run/docker.sock'
}

task dockerRun(type: Exec, dependsOn: 'dockerBuildImage', group: 'docker') {
    commandLine 'docker', 'run', '--rm', '-p', "${dockerPort}:${dockerPort}", dockerTagName, 'server'
}
